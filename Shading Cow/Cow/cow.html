<!DOCTYPE html>
<html>
    <title>Cow Shading</title>
    
<script id="vertex-shader" type="x-shader/x-vertex">

attribute vec4 vPosition; 
attribute vec3 vNormal;

uniform vec3 viewWorldPos;
uniform mat4 viewMatrix;
uniform mat4 projectionMatrix;
uniform mat4 uniMatrix;
uniform mat4 worldInverseTranspose;

uniform vec3 pointLightWorldPosition;
uniform vec3 spotLightWorldPosition;

varying vec3 fNormal;
varying vec3 v_surfaceToLightPoint;
varying vec3 v_surfaceToLightSpot;
varying vec3 surfaceToView;

void main()
{
    gl_Position = projectionMatrix * viewMatrix * uniMatrix * vPosition;

    fNormal = mat3(worldInverseTranspose) * vNormal;

    vec3 surfaceWorldPosition = (uniMatrix * vPosition).xyz;

    v_surfaceToLightPoint = pointLightWorldPosition - surfaceWorldPosition;
    v_surfaceToLightSpot = spotLightWorldPosition - surfaceWorldPosition;

    surfaceToView = viewWorldPos - surfaceWorldPosition;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">

// Adapted from Phong Demo by UofT: https://www.cs.toronto.edu/~jacobson/phong-demo/
precision mediump float;
uniform vec4 fColor;
uniform float spotLimit;
uniform vec3 spotLightDirection;

uniform float Ka;   // Ambient reflection coefficient
uniform float Kd;   // Diffuse reflection coefficient
uniform float Ks;   // Specular reflection coefficient
uniform float shininessVal; // shininess

// Material color
uniform vec3 ambientColor;
uniform vec3 diffuseColor;
uniform vec3 specularColor;

varying vec3 v_surfaceToLightPoint;
varying vec3 v_surfaceToLightSpot;
varying vec3 fNormal;
varying vec3 surfaceToView;

void main()
{
    // Normal
    vec3 normal = normalize(fNormal);
    // Pointlight
    vec3 surfaceToPointLightDirection = normalize(v_surfaceToLightPoint);
    float pointLight = max(dot(normal, surfaceToPointLightDirection), 0.0);
    // Spotlight
    vec3 surfaceToSpotLightDirection = normalize(v_surfaceToLightSpot);
    float dotFromSpotDirection = dot(surfaceToSpotLightDirection, -spotLightDirection);

    float lambertianPoint = max(dot(normal, surfaceToPointLightDirection), 0.0);
    float specularPoint = 0.0;

    float lambertianSpot = max(dot(normal, surfaceToSpotLightDirection), 0.0);
    float specularSpot = 0.0;

    if(lambertianPoint > 0.0) {
        vec3 R = reflect(-surfaceToPointLightDirection, normal);      // Reflected light vector
        vec3 V = normalize(surfaceToView); // Vector to viewer
        // Compute the specular term
        float specAngle = max(dot(R, V), 0.0);
        specularPoint = pow(specAngle, shininessVal);
    }
    float spotLight = 0.0;

    if (dotFromSpotDirection >= spotLimit) {
        spotLight = max(dot(normal, surfaceToSpotLightDirection), 0.0);
        if(lambertianSpot > 0.0) {
            vec3 R = reflect(-surfaceToSpotLightDirection, normal);      // Reflected light vector
            vec3 V = normalize(surfaceToView); // Vector to viewer
            // Compute the specular term
            float specAngle = max(dot(R, V), 0.0);
            specularSpot = pow(specAngle, shininessVal);
        }
    }
    float finalLight = min(pointLight + spotLight, 1.0); 
    gl_FragColor = vec4(Ka * ambientColor +
                      Kd * (lambertianPoint + lambertianSpot) * diffuseColor +
                      Ks * (specularPoint + specularSpot) * specularColor, 1.0); 
    gl_FragColor.rgb *= finalLight;
}
</script>

<script type="text/javascript" src="webgl-utils.js"></script>
<script type="text/javascript" src="initShaders.js"></script>
<script type="text/javascript" src="MV.js"></script>
<script type="text/javascript" src="cow.js"></script>

<body>
    <div>
        <canvas id="gl_canvas" width="1000" height="800">>
            Oops ... your browser doesn't support the HTML5 canvas element
        </canvas>
        <p id="Instructions">
            Instructions: <br> <br>
            Left Click Hold + Move Mouse = X/Y Pan/Translate Cow <br>           
            Right Click Hold + Move Mouse = X/Y Rotate Cow <br>
            Up/Down Arrow Keys = Z-Translate Cow <br>
            Left/Right ArrowKeys = Z-Rotate Cow <br>
            P = Turn Pointlight Rotation On/Off <br>
            S = Turn Spotlight Panning On/Off
        </p>
    </div>
</body>
</html>

<style>

    #Instructions{
        font-family: Arial, Helvetica, sans-serif;
        width: 1000px;
        font-weight: bold;
        text-align: center;
    }
</style>